$EXTERN WriteLine, Map, Fetch;

$LABEL S, E, T, Atom, Brackets, Alt;

//FROM Lexer
$EXTERN TkChar, TkNumber, TkName, TkVariable, TkOpenBracket, TkCloseBracket;
  
LinkBrackets {
  e.left (TkOpenBracket s.Num) e.body (TkCloseBracket s.Num) e.right =
    <LinkBrackets e.left> 
    (#Brackets <LinkBrackets e.body>)    
    <LinkBrackets e.right>;
  e.smth = e.smth;
}
  
ChangePatt {
  e.Pattern = 
    <LinkBrackets
      <Map
        {
          s.atom = (#Atom s.atom);
          (TkChar s.char) = (#Atom s.char);
          (TkNumber s.value) = (#Atom s.value);
          (TkName s.name) = (#Atom s.name);
          (TkVariable s.Mode e.Index) = 
            (<Fetch s.Mode { 'e' = #E; 't' = #T; 's' = #S; }> (e.Index) ());
          t.brackets = t.brackets;
        }
        e.Pattern
      >
    >;
}

MakePatterns {
  ((e.Pattern) (e.Result)) e.Tail = 
    (<ChangePatt e.Pattern>) 
    <MakePatterns e.Tail>;
  = ;
}

CreateHardSent {
  /* предлажение имеет вид (P') */
  (#Brackets e.body) ( e.tek_index ) = 
    (#Brackets <CreateHardSent e.body (e.tek_index)>);
    
  /* отщипнуть терм слева или справа */
  (#Brackets e.body) e.smth ( e.tek_index ) = 
    (#Brackets <CreateHardSent e.body (e.tek_index'l')>) 
    <CreateHardSent e.smth (e.tek_index'r')>;
    
  e.smth (#Brackets e.body) ( e.tek_index ) = 
    <CreateHardSent e.smth (e.tek_index'l')> 
    (#Brackets <CreateHardSent e.body (e.tek_index'r')>);
  
  /* переменные */
  (#S (e.name) (e.pattern)) e.smth ( e.tek_index ) = 
    (#S ('Index' e.tek_index) (#Alt ((#S (e.name) ()))))
    <CreateHardSent e.smth (e.tek_index'r')>;
  
  e.smth (#S (e.name) (e.pattern)) ( e.tek_index ) = 
    <CreateHardSent e.smth (e.tek_index'l')>
    (#S ('Index' e.tek_index) (#Alt ((#S (e.name) ()))));
  
  (#T (e.name) (e.pattern)) e.smth ( e.tek_index ) = 
    (#T ('Index' e.tek_index) (#Alt ((#T (e.name) ()))))
    <CreateHardSent e.smth (e.tek_index'r')>;
  
  e.smth (#T (e.name) (e.pattern)) ( e.tek_index ) = 
    <CreateHardSent e.smth (e.tek_index'l')>
    (#T ('Index' e.tek_index) (#Alt ((#T (e.name) ()))));
  
  /* атом */
  (#Atom e.attr) e.smth ( e.tek_index ) = 
    (#Atom e.attr)
    <CreateHardSent e.smth (e.tek_index'r')>;
  
  e.smth (#Atom e.attr) ( e.tek_index ) = 
    <CreateHardSent e.smth (e.tek_index'l')>
    (#Atom e.attr);
    
  /* пусто */
  ( e.tek_index ) = ;
  
  /* все остальное */
  e.smth ( e.tek_index ) = 
    (#E ('Index' e.tek_index) (#Alt (e.smth)));
}

$ENTRY GetFastGen {
  e.Sentence = 
    <WriteLine 
      <Map
        {
          (e.sent) = (<CreateHardSent e.sent ('_')>);
        }
        <MakePatterns e.Sentence>
      >
    >;
}