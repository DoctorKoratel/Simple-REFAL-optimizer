$FORWARD FastGen-Terms, FastGen-MeN, FastGen-M, Check;

$LABEL Brackets, Atom, S, T, E;

$EXTERN EraseBr;

$ENTRY FastGen {
	/* 0. Тело функции состоит из одного предложения */
	(e.1) = (e.1);
	
	/* 1. Предложения являются термами; cтроим БО согласно таблице */
	((#Brackets e.body1)) ((#Brackets e.body2)) e.Tail = 
		<FastGen 
			((#Brackets <FastGen-Terms (e.body1) (e.body2)>))
			e.Tail
		>;
	
	/* 2. Предложения класса (M,N) */
	((#Brackets e.left1) e.1 (#E (e.name1) (e.pattern1)) e.2 (#Brackets e.right1))
	((#Brackets e.left2) e.3 (#E (e.name2) (e.pattern2)) e.4 (#Brackets e.right2)) 
	e.Tail = 
		<FastGen 
			(<FastGen-MeN
				((#Brackets e.left1) e.1 (#E (e.name1) (e.pattern1)) e.2 
				(#Brackets e.right1)) ((#Brackets e.left2) e.3 (#E (e.name2) 
				(e.pattern2)) e.4 (#Brackets e.right2)) 
			>) 
			e.Tail
		>;
	
	/* 3. Предложения класса (M) */
	((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2) e.Tail = 
		<FastGen 
			(<FastGen-M 
				<Check 
					((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2)
				> 
				((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2)
			>) 
			e.Tail
		>;
	
	/* 4. Все остальное */
	(e.1) (e.2) = (#E ('Index') (#Alt (e.1) (e.2)));
}

$LABEL Alt;

FastGen-Terms {
	((#Brackets (#Brackets e.1))) ((#Brackets (#Brackets e.2))) = 
		(#Brackets <FastGen-Terms ((#Brackets e.1)) ((#Brackets e.2))>);
	
	((#Atom s.val)) ((#Atom s.val)) = (#Atom s.val);
	
	(#Atom s.v1) (#Atom s.v2) = (#S ('Index') ());
	
	(#S (e.name) (e.pattern)) (#Atom s.val) = 
		(#S (e.name) (#Alt (e.pattern) (s.val)));
	
	(#Atom s.val) (#S (e.name) (e.pattern)) = 
		(#S (e.name) (#Alt (e.pattern) (s.val)));
	
	(e.1) (e.2) = (#T ('Index') (#Alt (e.1) (e.2)));
}

FastGen-MeN {
	((#Brackets e.left1) e.1 (#E (e.name1) (e.pattern1)) e.2 (#Brackets e.right1))
	((#Brackets e.left2) e.3 (#E (e.name2) (e.pattern2)) e.4 (#Brackets e.right2)) 
	= 
			<FastGen 
				(#Brackets e.left1) (#Brackets e.left2)
			>
		
		<FastGen-MeN 
			(e.1 (#E (e.name1) (e.pattern1)) e.2)
			(e.3 (#E (e.name2) (e.pattern2)) e.4)
		> 
		 
			<FastGen 
				(#Brackets e.right1) (#Brackets e.right2)
			>
		;
	(e.1) (e.2) = (#E ('Index') (#Alt (e.1) (e.2)));
}

Check {
	((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2) = <Check (e.1) (e.2)>;
	() () = 1;
	e.1 = 0;
}

FastGen-M {
	1 ((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2) = 
		(<EraseBr 
			<FastGen 
				(e.left1) (e.left2)
			>
		> 
		<EraseBr 
			<FastGen-M 1 (e.1) (e.2)>
		>);
	1 () () = ;
	0 e.1 = (#E ('Index') (#Alt (e.1)));
}