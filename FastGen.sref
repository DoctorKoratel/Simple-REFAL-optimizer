$FORWARD FastGen-Terms, FastGen-MeN, FastGen-M, Check;

$LABEL Brackets, Atom, S, T, E;

$LABEL Alt;

$EXTERN EraseBr;

$ENTRY FastGen {
	/* 0. Тело функции состоит из одного предложения */
	(e.1) = (e.1);
	
	/* 1. Предложения являются термами; cтроим БО согласно таблице */
	((#Brackets e.body1)) ((#Brackets e.body2)) e.Tail = 
		<FastGen 
			(<FastGen-Terms ((#Brackets e.body1)) ((#Brackets e.body2))>)
			e.Tail
		>;
	
	/* 2. Предложения класса (M,N) */
	((#Brackets e.left1) e.1 (#E (e.name1) (e.pattern1)) e.2 (#Brackets e.right1))
	((#Brackets e.left2) e.3 (#E (e.name2) (e.pattern2)) e.4 (#Brackets e.right2)) 
	e.Tail = 
		<FastGen 
			(<FastGen-MeN
				((#Brackets e.left1) e.1 (#E (e.name1) (e.pattern1)) e.2 
				(#Brackets e.right1)) ((#Brackets e.left2) e.3 (#E (e.name2) 
				(e.pattern2)) e.4 (#Brackets e.right2)) 
			>)
			e.Tail
		>;
	
	/* 3. Предложения класса (M) */
	((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2) e.Tail = 
		<FastGen 
			(<FastGen-M 
				<Check 
					((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2)
				> 
				((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2)
			>) 
			e.Tail
		>;
	
	/* 4. Все остальное */
	(e.1) (e.2) = ((#E ('Index') (#Alt (e.1) (e.2))));
}

FastGen-Terms {
	((#Brackets (#Atom s.val))) ((#Brackets (#Atom s.val))) = 
		(#Brackets (#Atom s.val));
	
	((#Brackets (#Atom s.v1))) ((#Brackets (#Atom s.v2))) = 
		(#Brackets (#S ('Index') (#Alt ((#Atom s.v1)) ((#Atom s.v2)))));
	
	((#Brackets (#S (e.name) (e.pat)))) ((#Brackets (#Atom s.val))) = 
		(#Brackets (#S ('Index') (e.pat ((#Atom s.val)))));
	
	((#Brackets (#Atom s.val))) ((#Brackets (#S (e.name) (e.pat)))) = 
		(#Brackets (#S ('Index') (e.pat ((#Atom s.val)))));
	
	((#Brackets (#Brackets e.1))) ((#Brackets (#Brackets e.2))) =
		(#Brackets <FastGen-Terms ((#Brackets e.1)) ((#Brackets e.2))>);
	
	(e.1) (e.2) = (#T ('Index') (#Alt (e.1) (e.2)));
}

FastGen-MeN {
	((#Brackets e.left1) e.1 (#E (e.name1) (e.pattern1)) e.2 (#Brackets e.right1))
	((#Brackets e.left2) e.3 (#E (e.name2) (e.pattern2)) e.4 (#Brackets e.right2)) 
	= 
		<EraseBr <FastGen ((#Brackets e.left1)) ((#Brackets e.left2)) > >
		<FastGen-MeN 
				(e.1 (#E ('Index') (e.pattern1)) e.2)
				(e.3 (#E ('Index') (e.pattern2)) e.4)
		>
		<EraseBr <FastGen ((#Brackets e.right1)) ((#Brackets e.right2)) > >
		;
	(e.1) (e.2) = (#E ('Index') (#Alt (e.1) (e.2)));
}

Check {
	((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2) = <Check (e.1) (e.2)>;
	() () = 1;
	e.1 = 0;
}

FastGen-M {
	1 ((#Brackets e.left1) e.1) ((#Brackets e.left2) e.2) = 
		<FastGen-Terms ((#Brackets e.left1)) ((#Brackets e.left2))> 
		<FastGen-M 1 (e.1) (e.2)>;
	1 () () = ;
	0 (e.1) = (#E ('Index') (#Alt (e.1)));
}